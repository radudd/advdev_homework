#!groovy
podTemplate(
  label: "skopeo-pod",
  cloud: "openshift",
  inheritFrom: "maven",
  podRetention: always(),
  containers: [
    containerTemplate(
      name: "jnlp",
      image: "docker-registry.default.svc:5000/${GUID}-jenkins/jenkins-agent-appdev",
      resourceRequestMemory: "1Gi",
      resourceLimitMemory: "2Gi",
      resourceRequestCpu: "1",
      resourceLimitCpu: "2"
    )
  ]
) {
  node('skopeo-pod') {
    // Define Maven Command to point to the correct
    // settings for our Nexus installation
    def mvnCmd = "mvn -s ../nexus_settings.xml"
    def devProject = "${GUID}-tasks-dev"
    def prodProject = "${GUID}-tasks-prod"
    def appName = "tasks"
    def appDirName = "openshift-tasks"
    //def nexusUrl = "http://nexus.${GUID}-nexus.svc.cluster.local:8081"
    //def sonarqubeUrl = "http://sonarqube.${GUID}-sonarqube.svc.cluster.local:9000"
    //def nexusDockerRegistry = "nexus-registry-${GUID}-nexus.apps.na311.openshift.opentlc.com"
    def nexusUrl = "http://nexus3.gpte-hw-cicd.svc.cluster.local:8081"
    def sonarqubeUrl = "http://sonarqube-gpte-hw-cicd.apps.na311.openshift.opentlc.com"
    def nexusDockerRegistry = "nexus-registry-gpte-hw-cicd.apps.na311.openshift.opentlc.com"
    def ocpDockerRegistry = "docker-registry.default.svc:5000"

    // Checkout Source Code.
    stage('Checkout Source') {
      checkout scm
    }

    // Build the Tasks Service
    dir("${appDirName}") {
      // The following variables need to be defined at the top level
      // and not inside the scope of a stage - otherwise they would not
      // be accessible from other stages.
      // Extract version from the pom.xml
      def version = getVersionFromPom("pom.xml")

      // Set the tag for the development image: version + build number
      def devTag  = "${version}-" + currentBuild.number
      // Set the tag for the production image: version
      def prodTag = "${version}"

      // Using Maven build the war file
      // Do not run tests in this step
      stage('Build war') {
            echo "Building version ${devTag}"
            // Execute Maven Build
            sh "${mvnCmd} clean package -DskipTests"
      }

      // The next two stages should run in parallel
      stage('Running tests') {
          parallel (
            "Unit tests": {
                echo "Running Unit Tests"
                // Execute Unit Tests
                sh "${mvnCmd} test" 
              },
            "Code Analysis": {
                echo "Running Code Analysis"
                // Execute Sonarqube Tests
                sh "${mvnCmd} sonar:sonar -Dsonar.host.url=${sonarqubeUrl} \
                    -Dsonar.projectName=${JOB_BASE_NAME} \
                    -Dsonar.projectVersion=${devTag}" 
              }
          )
      }

      // Publish the built war file to Nexus
      stage('Publish to Nexus') {
        echo "Publish to Nexus"
        // Publish to Nexus
        sh "${mvnCmd} deploy -DskipTests=true -DaltDeploymentRepository=nexus::default::${nexusUrl}/repository/releases"
      }

      // Create the Build Config, if it doesn't exist
      stage('Creating BuildConfig') {
        // Build Image, tag Image
        openshift.withCluster() {
            openshift.withProject("${devProject}") {
                if (!openshift.selector('bc', "${appName}").exists()) {
                    echo "Creating BuildConfig resource for the application"
                    openshift.newBuild("--binary=true --name=${appName} jboss-eap71-openshift:1.3")
                }
            }
        }
      }

      // Build the OpenShift Image in OpenShift and tag it.
      stage('Build and Tag OpenShift Image') {
        echo "Building OpenShift container image tasks:${devTag}"
        openshift.withCluster() {
            openshift.withProject("${devProject}") {
                openshift.selector("bc", "${appName}").startBuild("--from-file=target/${appDirName}.war")
                
                def buildConfig = openshift.selector("bc","${appName}").object()
                def buildVersion = buildConfig.status.lastVersion
                def build = openshift.selector("build", "${appName}-${buildVersion}").object()
                echo "Waiting for Build to complete"
                while (build.status.phase != "Complete"){
                    if (build.status.phase == "Failed"){
                        error("Build failed")
                    }
                    sleep 5
                    build = openshift.selector("build", "${appName}-${buildVersion}").object()
                    echo "Current status: ${build.status.phase}"
                }
                openshift.tag("${appName}:latest","${appName}:${devTag}")
            }
        }
      }

      // Deploy the built image to the Development Environment.
      stage('Deploy to Dev') {
        echo "Deploying container image to Development Project"
        // Deploy to development Project
        //      Set Image, Set VERSION
        //      Make sure the application is running and ready before proceeding
        openshift.withCluster() {
            openshift.withProject("${devProject}") {
                if (openshift.selector("dc", "${appName}-dev").exists()) {
                    openshift.set("image", "dc/${appName}-dev", "${appName}-dev=${ocpDockerRegistry}/${devProject}/${appName}:${devTag}")
                    openshift.set("env", "dc/${appName}-dev", "VERSION='${devTag} (tasks-dev)'")
                } else {
                    openshift.newApp("--name=${appName}-dev", "--image-stream=${appName}:${devTag}") 
                    openshift.set("env", "dc/${appName}-dev", "VERSION='${devTag} (tasks-dev)'")
                    openshift.set("triggers", "dc/${appName}-dev", "--remove-all")
                    openshift.expose("svc", "${appName}-dev")
                }
            }
        }
      }

      // Copy Image to Nexus container registry
      stage('Copy Image to Nexus container registry') {
        echo "Copy image to Nexus container registry"

        // Copy image to Nexus container registry
        sh "skopeo copy --src-tls-verify=false --dest-tls-verify=false \
                        --src-creds openshift:\$(oc whoami -t) --dest-creds admin:redhat \
                        docker://${ocpDockerRegistry}/${devProject}/${appName}:${devTag} \
                        docker://${nexusDockerRegistry}/${appName}:${devTag}"

        // Tag the built image with the production tag.
        openshift.withCluster() {
            openshift.withProject("${devProject}") {
                openshift.tag("${devProject}/${appName}:${devTag}", "${devProject}/${appName}:${prodTag}")
            }
        }

      }

      // Blue/Green Deployment into Production
      // -------------------------------------
      def activeApp = ""
      def destApp   = ""

      stage('Blue/Green Production Deployment') {
        // Determine which application is active
        //      Set Image, Set VERSION
        //      Deploy into the other application
        //      Make sure the application is running and ready before proceeding
        openshift.withCluster() {
            openshift.withProject("${prodProject}") {

                openshift.set("env", "dc/${appName}-blue", "VERSION='${prodTag} (tasks-blue)'")
                openshift.set("env", "dc/${appName}-green", "VERSION='${prodTag} (tasks-green)'")

                activeApp = openshift.selector("route", "${appName}").object().spec.to.name
                if (activeApp == "${appName}-green") {
                    destApp = "${appName}-blue"
                } else {
                    destApp = "${appName}-green"
                }

                echo "Active app: " + activeApp
                echo "Dest app: " + destApp

                openshift.set("image", "dc/${destApp}", "${destApp}=${ocpDockerRegistry}/${devProject}/${appName}:${prodTag}")

                def dcProd = openshift.selector("dc","${destApp}")
                dcProd.rollout().latest()

                def rcProdVersion = dcProd.object().status.latestVersion
                def rcProd = openshift.selector("rc","${destApp}-${rcProdVersion}").object()
                echo "Waiting for ${destApp} to be ready"
                while (rcProd.status.readyReplicas != rcProd.spec.replicas) {
                    sleep 10
                    rcProd = openshift.selector("rc", "${destApp}-${rcProdVersion}").object()
                }
            }
        }
      }

      stage('Switch over to new Version') {
        input "Switch production?"
        echo "Switching Production application to ${destApp}."
        // Execute switch
        openshift.withCluster() {
            openshift.withProject("${prodProject}") {
                def prodRoute = openshift.selector("route", "${appName}").object()
                prodRoute.spec.to.name = "${destApp}"
                openshift.apply(prodRoute)
            }
        }
      }
    }
  }
}

// Convenience Functions to read version from the pom.xml
// Do not change anything below this line.
// --------------------------------------------------------
def getVersionFromPom(pom) {
  def matcher = readFile(pom) =~ '<version>(.+)</version>'
  matcher ? matcher[0][1] : null
}